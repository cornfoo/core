list:
    just --list

build:
  cargo build

install-ios-targets:
    echo "==> Installing iOS targets..."
    rustup target add aarch64-apple-ios-sim
    rustup target add aarch64-apple-ios

build-ios: build-targets bindgen-swift create-frameworks xcframework cp-xcframework-source

export LIB_NAME := "juvycore"
export FW_NAME := "JuvyCore"
export STATIC_LIB_NAME := "lib" + LIB_NAME + ".a"
export DY_LIB_NAME := if os() == "macos" { "libjuvycore.dylib" } else { "libjuvycore.so" }

export BUILD_MODE_ENV := env_var_or_default("BUILD_MODE", "debug")
export BUILD_MODE_TARGET := if BUILD_MODE_ENV == "release" { "release" } else { "debug" }
export BUILD_FLAG := if BUILD_MODE_TARGET == "release" { "--release" } else { "" }

export TARGET_DIR := "../target"
export DEPLOYMENT_TARGET := env_var_or_default("IPHONEOS_DEPLOYMENT_TARGET", "17.4")
export GEN_SWIFT_FOLDER := "generated/swift"

export FW_FFI_NAME := FW_NAME + "FFI"
export FW_FFI_FILE := FW_FFI_NAME + ".framework"
export XC_FW_FFI_NAME := FW_FFI_NAME + ".xcframework"
export TARGET_XC_FW_FOLDER := "target/spm"
export LIB_OUTPUT_PATH := TARGET_DIR + "/" + BUILD_MODE_TARGET + "/" + DY_LIB_NAME

build-targets:
    #!/usr/bin/env bash
    echo "==> Building iOS deployment target: ${DEPLOYMENT_TARGET}, mode: ${BUILD_MODE_TARGET}"
    IPHONEOS_DEPLOYMENT_TARGET=${DEPLOYMENT_TARGET} cargo build --timings \
        --target aarch64-apple-ios \
        --target aarch64-apple-ios-sim \
        ${BUILD_FLAG}

bindgen-swift:
    #!/usr/bin/env bash
    echo "==> Bindgen swift ${STATIC_LIB_NAME}"
    mkdir -p ${GEN_SWIFT_FOLDER}
    
    if [ "${BUILD_MODE_TARGET}" == "release" ]; then \
        export RUSTFLAGS="${RUSTFLAGS:-} -C strip=none"; \
    fi
    
    cargo build ${BUILD_FLAG}
    
    if [ ! -f "${LIB_OUTPUT_PATH}" ]; then \
        echo "Failed to locate ${DY_LIB_NAME} at ${LIB_OUTPUT_PATH} (mode ${BUILD_MODE_TARGET})"; \
        exit 1; \
    fi

    cargo run -p xtask -- generate \
        --library "${LIB_OUTPUT_PATH}" \
        --language swift \
        --crate "${LIB_NAME}" \
        --out-dir "${GEN_SWIFT_FOLDER}"

create-frameworks:
    #!/usr/bin/env bash
    echo "==> Creating framework bundles"
    set -euo pipefail

    JUVYCORE_DIR=$(pwd)

    MODULE_MAP="$(printf '%s\n' \
    "// Auto-generated module map for the JuvyCore framework." \
    "framework module ${FW_FFI_NAME} {" \
    "    header \"${FW_FFI_NAME}.h\"" \
    "    export *" \
    "}")"

    cd ${TARGET_DIR}
    find . -type d -name "${FW_FFI_FILE}" -exec rm -rf {} + 2>/dev/null || true
    
    targets=(aarch64-apple-ios aarch64-apple-ios-sim)
    for target in "${targets[@]}"; do
        echo "==> Creating framework for ${target}"
        framework_dir="${TARGET_DIR}/${target}/${BUILD_MODE_TARGET}/${FW_FFI_FILE}"
        
        mkdir -p "${framework_dir}/Headers"
        mkdir -p "${framework_dir}/Modules"
        
        echo "${MODULE_MAP}" > "${framework_dir}/Modules/module.modulemap"
        cp "${JUVYCORE_DIR}/${GEN_SWIFT_FOLDER}/${FW_FFI_NAME}.h" "${framework_dir}/Headers/"
        cp "${TARGET_DIR}/${target}/${BUILD_MODE_TARGET}/${STATIC_LIB_NAME}" "${framework_dir}/${FW_FFI_NAME}"
        cp "${JUVYCORE_DIR}/src/Info.plist" "${framework_dir}/"
    done

xcframework:
    #!/usr/bin/env bash
    echo "==> Creating XCFramework"
    set -euo pipefail

    rm -rf ${TARGET_XC_FW_FOLDER}/Sources/${XC_FW_FFI_NAME} && mkdir -p ${TARGET_XC_FW_FOLDER}/Sources

    xcodebuild -create-xcframework \
        -framework ${TARGET_DIR}/aarch64-apple-ios/${BUILD_MODE_TARGET}/${FW_FFI_FILE} \
        -framework ${TARGET_DIR}/aarch64-apple-ios-sim/${BUILD_MODE_TARGET}/${FW_FFI_FILE} \
        -output ${TARGET_XC_FW_FOLDER}/Sources/${XC_FW_FFI_NAME}

cp-xcframework-source:
    #!/usr/bin/env bash
    mkdir -p ${TARGET_XC_FW_FOLDER}/Sources/${FW_NAME}
    cp src/Package.swift ${TARGET_XC_FW_FOLDER}
    cp ${GEN_SWIFT_FOLDER}/${FW_NAME}.swift ${TARGET_XC_FW_FOLDER}/Sources/${FW_NAME}